# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=('libogg' 'libogg-devel')
pkgver=1.3.3
pkgrel=1
pkgdesc='Ogg bitstream and framing library'
arch=('i686' 'x86_64')
license=('BSD')
url="https://www.xiph.org/"
depends=('msys2-runtime')
makedepends=('msys2-runtime-devel')
options=(!libtool)
source=("https://downloads.xiph.org/releases/ogg/${pkgname}-${pkgver}.tar.xz")
sha256sums=('4f3fc6178a533d392064f14776b23c397ed4b9f48f5de297aba73b643f955c08')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --enable-static
  make
  make install DESTDIR="${srcdir}/dest"
}

package_libogg() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_libogg-devel() {
  pkgdesc="libogg headers and libraries"
  depends=("libogg=${pkgver}")

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
