# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=libxt
pkgver=1.1.5
pkgrel=1
pkgdesc="X11 toolkit intrinsics library"
arch=('i686' 'x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libsm' 'libx11')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/lib/libXt-${pkgver}.tar.bz2)
sha256sums=('46eeb6be780211fdd98c5109286618f6707712235fdd19df4ce1e6954f349f1a')

prepare() {
  cd libXt-${pkgver}
  autoreconf -fiv
}

build() {
  cd libXt-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --enable-shared --enable-static
  make
}

package() {
  cd libXt-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
