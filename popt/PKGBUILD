# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=('popt' 'popt-devel')
pkgver=1.16
pkgrel=1
pkgdesc="C library for parsing command line parameters"
arch=('i686' 'x86_64')
url="http://www.rpm5.org/"
license=("MIT")
makedepends=("gcc"
             "pkg-config"
             "gettext-devel"
             "msys2-runtime-devel")
depends=("msys2-runtime"
         "gettext"
         "libintl")
options=('staticlibs' 'strip' '!libtool')
source=(#"http://rpm5.org/files/popt/popt-${pkgver}.tar.gz"
        ftp://anduin.linuxfromscratch.org/BLFS/popt/popt-${pkgver}.tar.gz
        "197416.all.patch"
        "217602.all.patch"
        "278402-manpage.all.patch"
        "318833.all.patch"
        "356669.all.patch"
        "367153-manpage.all.patch"
        "popt-1.16-msys.patch"
        "popt-1.16-glob-pattern.patch"
        )
sha256sums=('e728ed296fe9f069a0e005003c3d6b2dde3d9cad453422a10d6558616d304cc8'
            '38ea3b1e9b49129c666bdb58afeaa7648336148eaaec96fb52850edb5b351d7e'
            '6554fae42ea6117747df66879872e59746ff8f44130f678c9b99fa23580107c7'
            '99b9b5547edfac75eddb90083e3d19c0076d25f1d3e1ad165b49af81ecad8ce9'
            '6a2d7d78a391041e8fecd8b8c952ac4a4d5368ff006ec37b21551ee23dd99362'
            '3acd9334d967ea84ef75da2fdcb8b9efc4301b9755e1773fd6a4488c7b77ef61'
            'eb885494ee5b9e8e904c1235b38bac28f248d236728cb28f22f99aee93c46989'
            '9d273dac5d65e14d7e08e1c92035f22d9cfc034f4f7a2c375cb6493fa4023194'
            '29794b7bf8406a1f354108e7fb9fbb29c86b6f6ac5d6ab7794133f32616227bd')

prepare() {
  cd "${srcdir}/${pkgname}-$pkgver"
  rm -rf po/ca.po || true
  patch -p1 -i $srcdir/197416.all.patch
  patch -p1 -i $srcdir/217602.all.patch
  patch -p1 -i $srcdir/278402-manpage.all.patch
  patch -p1 -i $srcdir/318833.all.patch
  patch -p1 -i $srcdir/356669.all.patch
  patch -p1 -i $srcdir/367153-manpage.all.patch
  patch -Np1 -i "${srcdir}/popt-1.16-msys.patch"
  patch -Np1 -i "${srcdir}/popt-1.16-glob-pattern.patch"
  
  autoreconf -fiv
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr
  make
  make install DESTDIR="${srcdir}/dest"
  install -D -m644 "COPYING" "${srcdir}/dest/usr/share/licenses/${pkgname}/LICENSE"
}

package_popt() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_popt-devel() {
  pkgdesc="popt headers and libraries"
  depends=("popt=${pkgver}")

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
