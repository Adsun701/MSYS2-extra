# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=dmxproto
pkgver=2.3.1
pkgrel=1
pkgdesc="X11 Distributed Multihead X extension wire protocol"
arch=('any')
makedepends=('xorg-util-macros')
url="https://xorg.freedesktop.org/"
license=('custom')
source=("${url}/releases/individual/proto/${pkgname}-${pkgver}.tar.bz2"
        "0001-dmxproto-2.3.1-fix-build-msys2.patch")
sha1sums=('3b8b273b8ef3d8dbab998df9ec1dddf99edf4d91'
          'aab6a7595c52a52c7a42a3e21654c29d101584b0')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-dmxproto-2.3.1-fix-build-msys2.patch"
  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
