# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=('libdaemon' 'libdaemon-devel')
pkgver=0.14
pkgrel=1
pkgdesc="Lightweight C library that eases the writing of UNIX daemons"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://0pointer.de/lennart/projects/libdaemon/"
depends=('msys2-runtime')
makedepends=('msys2-runtime-devel' 'git')
options=(!libtool)
source=("git://git.0pointer.net/libdaemon.git#commit=9fcc28e0e8f84968d1fb8b6d544a42efb13803ec"
        "libdaemon-0.14-no-undefined.patch")
sha256sums=('SKIP'
            '0cc7de36a970cf188e3f442da9a3b102aac109c5874580876d7ad3bdcbbdc747')

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -Np1 -i "${srcdir}/libdaemon-0.14-no-undefined.patch"
  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}"
  ./configure --prefix=/usr --localstatedir=/var --disable-lynx
  make
  make install DESTDIR="${srcdir}/dest"
}


package_libdaemon() {
  cd "${srcdir}/${pkgname}"

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libdaemon-devel() {
  pkgdesc="libdaemon headers and libraries"
  depends=("libdaemon=${pkgver}")

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
