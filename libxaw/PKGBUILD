# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=libxaw
pkgver=1.0.13
pkgrel=1
pkgdesc="X11 Athena Widget library"
arch=('i686' 'x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libxmu' 'libxpm')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/lib/libXaw-${pkgver}.tar.bz2"
        "0001-libxaw-1.0.13-fix-msys2-installation.patch")
sha256sums=('8ef8067312571292ccc2bbe94c41109dcf022ea5a4ec71656a83d8cce9edb0cd'
            '21578fb1088311c73a7d7f9320c6ae5ecd1d7cb3e268b87065b0518053101ab5')

prepare() {
  cd libXaw-${pkgver}
  patch -Np1 -i "${srcdir}/0001-libxaw-1.0.13-fix-msys2-installation.patch"
  autoreconf -fiv
}

build() {
  cd libXaw-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --enable-shared --enable-static
  make
}

package() {
  cd libXaw-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
