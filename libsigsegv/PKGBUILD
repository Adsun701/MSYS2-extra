# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=('libsigsegv' 'libsigsegv-devel')
pkgver=2.12
pkgrel=1
pkgdesc='Page fault detection library'
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.gnu.org/software/libsigsegv/"
depends=('msys2-runtime' 'gcc-libs')
makedepends=('msys2-runtime-devel' 'gcc')
options=(!libtool)
source=("${pkgname}-${pkgver}.tar.gz"::"http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "0001-libsigsegv-2.11-fix-assembly.patch")
sha256sums=('3ae1af359eebaa4ffc5896a1aee3568c052c99879316a1ab57f8fe1789c390b6'
            '73d963f144531fad281ca3f4f374c1e328cc505080ef135e8728f114e5aec371')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-libsigsegv-2.11-fix-assembly.patch"
  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --enable-static
  make
  make install DESTDIR="${srcdir}/dest"
}

package_libsigsegv() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
}

package_libsigsegv-devel() {
  pkgdesc="libsigsegv headers and libraries"
  depends=("libsigsegv=${pkgver}")

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
