# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=('libvorbis' 'libvorbis-devel')
pkgver=1.3.6
pkgrel=1
pkgdesc='Vorbis codec library'
arch=('i686' 'x86_64')
license=('custom')
url="https://www.xiph.org/"
depends=('msys2-runtime' 'libogg' 'gcc-libs')
makedepends=('msys2-runtime-devel' 'libogg-devel' 'gcc' 'pkg-config')
options=(!libtool)
source=("https://downloads.xiph.org/releases/vorbis/${pkgname}-${pkgver}.tar.xz")
sha256sums=('af00bb5a784e7c9e69f56823de4637c350643deedaf333d0fa86ecdba6fcb415')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --enable-static
  make
  make install DESTDIR="${srcdir}/dest"
}

package_libvorbis() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_libvorbis-devel() {
  pkgdesc="libvorbis headers and libraries"
  depends=("libvorbis=${pkgver}")

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
