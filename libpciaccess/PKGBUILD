# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=libpciaccess
pkgver=0.14
pkgrel=1
pkgdesc="X11 PCI access library"
arch=('i686' 'x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('msys2-runtime')
makedepends=('xorg-util-macros' 'msys2-runtime-devel')
source=("${url}/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2"
        "0001-libpciaccess-0.14-msys2.patch")
sha512sums=('bf40214dfd22f2a223f7c948566e6eaa4bfed60116e71b3eb19f6fe7ab2c4546649eb0416c06d5983e476ea5e27de036936646eb5f800ea97763b6d1f1976f4b'
            'bdb8cf85b306b72045c342d46076ee3c36dbcc2ba45b4be7abb59cafd482f1b1f054c9c8d599df7335bfcaa39cb389f8114aa284e77139ce65ff5358a2e6c35c')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i "${srcdir}/0001-libpciaccess-0.14-msys2.patch"
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
