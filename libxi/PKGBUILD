# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=libxi
pkgver=1.7.9
pkgrel=1
pkgdesc="X11 Input extension library"
arch=('i686' 'x86_64')
url="https://xorg.freedesktop.org"
depends=('libxext' 'inputproto')
makedepends=('pkgconfig' 'xorg-util-macros' 'libxfixes' 'automake')
license=('custom')
source=(${url}/releases/individual/lib/libXi-${pkgver}.tar.bz2)
sha256sums=('c2e6b8ff84f9448386c1b5510a5cf5a16d788f76db018194dacdc200180faf45')

prepare() {
  cd libXi-${pkgver}
  autoreconf -fiv
}

build() {
  cd libXi-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --enable-shared --enable-static
  make
}

package() {
  cd libXi-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
