# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=libx11
pkgver=1.6.5
pkgrel=1
pkgdesc="X11 client-side library"
arch=('i686' 'x86_64')
url="https://xorg.freedesktop.org/"
depends=('libxcb' 'xproto' 'kbproto')
makedepends=('xorg-util-macros' 'xextproto' 'xtrans' 'inputproto')
license=('custom')
source=("${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2")
sha256sums=('4d3890db2ba225ba8c55ca63c6409c1ebb078a2806de59fb16342768ae63435d')

prepare() {
  cd "${srcdir}/libX11-${pkgver}"
  autoreconf -fiv
}

build() {
  cd "${srcdir}/libX11-${pkgver}"
  CPPFLAGS+=" -include /usr/include/xlocale.h" ./configure --prefix=/usr --enable-shared --enable-static --disable-xf86bigfont
  make VERBOSE=1
}

check() {
  cd "${srcdir}/libX11-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/libX11-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
