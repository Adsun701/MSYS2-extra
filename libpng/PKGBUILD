# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=('libpng' 'libpng-devel')
pkgver=1.6.35
_apngver=1.6.35
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686' 'x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib')
makedepends=('gcc' 'zlib-devel')
options=('!libtool')
source=("https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        "https://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${_apngver}-apng.patch.gz")
sha256sums=('23912ec8c9584917ed9b09c5023465d71709dce089be503c7867fec68a93bcd7'
            '18ceb239857bbd32ca706cdad29f1a5dcfb87a5c54258db3bb077cff78c12e04')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Add animated PNG (apng) support. Required by Firefox
  # see http://sourceforge.net/projects/libpng-apng/
  patch -Np1 -i "${srcdir}/libpng-${_apngver}-apng.patch"
  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-static --enable-shared
  make
  make install DESTDIR="${srcdir}/dest"
  
  cd contrib/pngminus
  make PNGLIB="-L${srcdir}/dest/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "${srcdir}/dest/usr/bin/"
  install -D -m644 ../../LICENSE "${srcdir}/dest/usr/share/licenses/${pkgname}/LICENSE"
}

package_libpng() {
  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libpng-devel() {
  pkgdesc="libpng headers and libraries"
  depends=("libpng=${pkgver}")
  
  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
