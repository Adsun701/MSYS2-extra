# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=libxdmcp
pkgver=1.1.2
pkgrel=1
pkgdesc="X11 Display Manager Control Protocol library"
arch=('i686' 'x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('xproto' 'msys2-runtime')
makedepends=('xorg-util-macros' 'msys2-runtime-devel')
source=(${url}/releases/individual/lib/libXdmcp-${pkgver}.tar.bz2)
sha256sums=('81fe09867918fff258296e1e1e159f0dc639cb30d201c53519f25ab73af4e4e2')

prepare() {
  cd libXdmcp-${pkgver}
  autoreconf -fiv
}

build() {
  cd libXdmcp-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --enable-shared --enable-static
  make
}

package() {
  cd libXdmcp-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
