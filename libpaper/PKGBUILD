# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=libpaper
pkgver=1.1.24
pkgrel=1
pkgdesc='Library for handling paper characteristics'
arch=('i686' 'x86_64')
url='https://packages.debian.org/unstable/source/libpaper'
license=('GPL')
depends=('sh')
makedepends=('gettext')
source=("http://ftp.de.debian.org/debian/pool/main/libp/$pkgname/${pkgname}_$pkgver+nmu5.tar.gz"
        "0001-libpaper-1.1.24-no-undefined.patch")
sha256sums=('e29deda4cd7350189c71af0925cbf4a4473f9841d1419a922e1e8ff1954db1f2'
            'c745dd199596e089888d99d9694695f59467b17aa7c0d7323d4a2ab20532a9ea')

prepare() {
  cd "$pkgname-$pkgver+nmu5"
  patch -Np1 -i "${srcdir}/0001-libpaper-1.1.24-no-undefined.patch"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver+nmu5"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin
  make
}

package() {
  cd "$pkgname-$pkgver+nmu5"
  make DESTDIR="$pkgdir" install
  
  # add systemwide default papersize read by many office applications
  install -dm 755 $pkgdir/etc
  echo '# Simply write the paper name. See papersize(5) for possible values' > $pkgdir/etc/papersize

  # add libpaper.d directory other packages can use to store files
  install -dm 755 $pkgdir/etc/libpaper.d

  # add localisation
  pushd debian/po
  for i in `ls *.po`; do
	install -dm 755 ${pkgdir}/usr/share/locale/${i%.po}/LC_MESSAGES/;
	msgfmt $i -o ${pkgdir}/usr/share/locale/${i%.po}/LC_MESSAGES/${pkgname}.mo;
  done
  popd
}
