# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=libxau
pkgver=1.0.8
pkgrel=1
pkgdesc="X11 authorisation library"
arch=('i686' 'x86_64')
url="https://xorg.freedesktop.org/"
depends=('msys2-runtime' 'xproto>=7.0.18')
makedepends=('pkgconfig' 'msys2-runtime-devel')
license=('custom')
source=(${url}/releases/individual/lib/libXau-${pkgver}.tar.bz2)
sha256sums=('fdd477320aeb5cdd67272838722d6b7d544887dfe7de46e1e7cc0c27c2bea4f2')

prepare() {
  cd libXau-${pkgver}
  autoreconf -fiv
}

build() {
  cd libXau-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --enable-shared --enable-static
  make
}

package() {
  cd libXau-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
