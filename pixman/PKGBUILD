# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=('pixman' 'pixman-devel')
pkgver=0.34.0
pkgrel=1
pkgdesc="The pixel-manipulation library for X and cairo"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org"
license=('custom')
depends=('msys2-runtime' 'libpng')
makedepends=('msys2-runtime-devel' 'libpng-devel')
source=("https://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('367698744e74d6d4f363041482965b9ea7fbe4a5')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-shared --enable-static
  make
  make install DESTDIR="${srcdir}/dest"
  install -m755 -d "${srcdir}/dest/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${srcdir}/dest/usr/share/licenses/${pkgname}/"
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package_pixman() {
  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_pixman-devel() {
  pkgdesc="pixman headers and libraries"
  depends=("pixman=${pkgver}")
  
  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
